buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/groups/public/' }
	}
	dependencies {
		classpath 'org.ajoberstar:gradle-git:0.6.2'
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.6.0'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
	}
}

group = 'org.ajoberstar'
description = 'Groovy layer over JGit.'
version = '0.1.0'

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'github-pages'
apply from: rootProject.file('gradle/publishing.gradle')
apply from: rootProject.file('gradle/license.gradle')

sourceCompatibility = '1.7'

repositories { mavenCentral() }

def jgitVersion = '3.1.0.201310021548-r'

dependencies {
	compile localGroovy()
	compile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
	compile "org.eclipse.jgit:org.eclipse.jgit.ui:$jgitVersion"

	['jsch', 'pageant', 'sshagent', 'usocket-jna'].each {
		compile "com.jcraft:jsch.agentproxy.${it}:0.0.6"
	}

	compile 'org.slf4j:slf4j-api:1.7.2'

	testCompile 'junit:junit:4.10'
	testCompile('org.spockframework:spock-core:0.7-groovy-1.8') { exclude group: 'org.codehaus.groovy' }
}

task sourcesJar(type:Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
	classifier = 'javadoc'
	from javadoc.outputs.files
}

task groovydocJar(type:Jar) {
	classifier = 'groovydoc'
	from groovydoc.outputs.files
}

githubPages {
	repoUri = 'git@github.com:ajoberstar/grgit.git'
	pages {
		from 'src/gh-pages'
		from(javadoc.outputs.files) {
			into 'docs/javadoc'
		}
		from(groovydoc.outputs.files) {
			into 'docs/groovydoc'
		}
	}
}

wrapper {
	gradleVersion = '1.8'
}
