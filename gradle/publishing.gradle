apply plugin: 'maven-publish'
apply plugin: 'bintray'

publishing {
	publications {
		main(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			artifact groovydocJar

			pom.withXml {
				asNode().with {
					appendNode('name', 'grgit')
					appendNode('description', 'The Groovy way to use Git.')
					appendNode('url', 'http://github.com/ajoberstar/grgit')
					appendNode('licenses').with {
						appendNode('license').with {
							appendNode('name', 'The Apache Software License, Version 2.0')
							appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0')
						}
					}
					appendNode('developers').with {
						appendNode('developer').with {
							appendNode('id', 'ajoberstar')
							appendNode('name', 'Andrew Oberstar')
							appendNode('email', 'andrew@ajoberstar.org')
						}
					}
					appendNode('scm').with {
						appendNode('connection', 'git://github.com/ajoberstar/grgit.git')
						appendNode('developerConnection', 'git@github.com:ajoberstar/grgit.git')
						appendNode('url', 'http://github.com/ajoberstar/grgit')
					}
				}
			}
		}
	}
	repositories {
		mavenLocal()
	}
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
	bintray {
		user = project.bintrayUser
		key = project.bintrayKey
		publications = ['main']
		pkg {
			repo = 'libraries'
			name = 'org.ajoberstar:grgit'
			desc = 'The Groovy way to use Git.'
			licenses = ['Apache-2.0']
			labels = ['groovy', 'git']
		}
	}
}
