plugins {
  id 'org.jbake.site'
  id 'groovy'
}

repositories {
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots/'
  }
}

dependencies {
  // groovy
  implementation 'org.codehaus.groovy:groovy:[2.5.0, 2.6.0-alpha)'

  // jgit
  compile 'org.eclipse.jgit:org.eclipse.jgit:latest.release'
  compile 'org.eclipse.jgit:org.eclipse.jgit.ui:latest.release'

  // agentproxy
  implementation 'com.jcraft:jsch.agentproxy.core:latest.release'
  implementation 'com.jcraft:jsch.agentproxy.jsch:latest.release'
  implementation 'com.jcraft:jsch.agentproxy.pageant:latest.release'
  implementation 'com.jcraft:jsch.agentproxy.sshagent:latest.release'
  implementation 'com.jcraft:jsch.agentproxy.usocket-jna:latest.release'
  implementation 'com.jcraft:jsch.agentproxy.usocket-nc:latest.release'

  constraints {
    implementation('com.jcraft:jsch:[0.1.54,)') {
      because 'agentproxy pulls this in, but we need fixes from a newer version'
    }
  }

  // logging
  testImplementation 'org.slf4j:slf4j-api:latest.release'
  testRuntime 'org.slf4j:slf4j-simple:latest.release'

  // testing
  testImplementation 'junit:junit:latest.release'
  testImplementation('org.spockframework:spock-core:1.2-groovy-2.5-SNAPSHOT') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-xml'
  }
}

test {
  useJUnit {
    excludeCategories 'org.ajoberstar.grgit.fixtures.PlatformSpecific'
  }
}

['Windows', 'Linux'].each { platform ->
  tasks.create("test${platform}", Test) {
    useJUnit {
      includeCategories "org.ajoberstar.grgit.fixtures.${platform}Specific"
    }
    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'debug'
  }
}

jbake {
  srcDirName = 'docs'
}

rootProject.gitPublish {
  contents {
    from "${buildDir}/${jbake.destDirName}"
  }
}

rootProject.gitPublishCopy.dependsOn bake
